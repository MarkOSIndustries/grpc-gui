allprojects {
  group 'msg'
  version '1.0.0'
}

def kotlinProjects() {
  def result = []
  getTasksByName('compileKotlin', true).forEach{task -> result.add(task.project)}
  return result
}

configure(kotlinProjects()) {
  apply plugin: 'java' // needed for ktlint

  repositories {
    mavenCentral() // needed for ktlint
  }

  configurations {
    ktlint
  }

  dependencies {
    ktlint group: 'com.pinterest', name: 'ktlint', version: '0.41.0'
  }

  task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
  }
  check.dependsOn ktlint

  task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
  }
}
